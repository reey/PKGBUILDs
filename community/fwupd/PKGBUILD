# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove depends on libsmbios, efivar
#  - remove makedepends on valgrind, gnu-efi-libs
#  - meson with -Dplugin_dell=false (smbios dependency)
#  - meson with -Dplugin_uefi=false -Dplugin_redfish=false -Dplugin_nvme=false (efivar, gnu-efi-libs dependency)

pkgname=fwupd
pkgver=1.2.1
pkgrel=1
pkgdesc="A simple daemon to allow session software to update firmware"
arch=('x86_64')
url="https://github.com/hughsie/fwupd"
license=('LGPL')
depends=('libxmlb' 'python' 'libgusb'
         'libsoup' 'json-glib' 'gcab' 'libarchive' 'gpgme'
         'libgudev' 'polkit' 'shared-mime-info')
makedepends=('meson' 'gobject-introspection' 'gtk-doc'
             'python-cairo' 'noto-fonts' 'noto-fonts-cjk' 'python-gobject' 'vala'
             'bash-completion' 'python-pillow' 'help2man')
checkdepends=('umockdev')
backup=('etc/fwupd/daemon.conf' 'etc/fwupd/uefi.conf')
source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('e1c1d629d09756388f28e2e769443dad918cb9a188898ab5547e3e02351dcb88' 'SKIP')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17') # Richard Hughes <richard@hughsie.com>

build() {
    cd ${pkgname}-${pkgver}
    arch-meson -D b_lto=false -Dplugin_dell=false -Dplugin_uefi=false -Dplugin_redfish=false -Dplugin_nvme=false ../build
    ninja -v -C ../build
}

check() {
    cd build
    meson test
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install
    # Fixup mode to match polkit
    install -d -o root -g 102 -m 750 "${pkgdir}/usr/share/polkit-1/rules.d"
    # Remove the tests
    rm -r ${pkgdir}/usr/share/installed-tests/
}
