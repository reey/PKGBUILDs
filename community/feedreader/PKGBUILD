# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Andrew O'Neill <andrew at meanjollies dot com>
# Contributor: poisonby <poisonby@tutanota.com>
# Contributor: Jason Scurtu (scujas) <jscurtu@gmail.com>
# Contributor: Marcin Tydelski <marcin.tydelski@gmail.com>
# Contributor: Jan Lukas Gernert (JeanLuc) <https://launchpad.net/~eviltwin1>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix finding libcurl on arm

pkgname=feedreader
_pkgname=FeedReader
pkgver=2.6.1
pkgrel=1
pkgdesc="Modern desktop application designed to complement existing web-based RSS accounts"
arch=(x86_64)
url="https://jangernert.github.io/FeedReader/"
license=(GPL3)
depends=(curl gnome-keyring gnome-online-accounts gumbo-parser libgee libpeas)
makedepends=(git gobject-introspection meson vala)
_commit=3cc7df1812ddbd484ac1493d178d1ad58b389d4b  # tags/v2.6.1
source=("git+https://github.com/jangernert/$_pkgname#commit=$_commit"
        '0001-fix-finding-curl.patch')
sha256sums=('SKIP'
            'e6a2c27346d8679d11c09890ad38971ea640254537a3b2ac12beb84f393d04ef')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $_pkgname
  patch -p1 -i ../0001-fix-finding-curl.patch
}

build() {
  arch-meson $_pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
