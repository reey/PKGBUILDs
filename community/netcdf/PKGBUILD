# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: damir <damir@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable CDFS for 32-bit ARM

pkgname=netcdf
pkgver=4.6.2
pkgrel=1
pkgdesc="network Common Data Form interface for array-oriented data access and corresponding library"
arch=('x86_64')
url="https://www.unidata.ucar.edu/software/netcdf/"
depends=('hdf5' 'curl')
makedepends=('cmake')
optdepends=('netcdf-fortran: fortran bindings' 'netcdf-cxx: c++ bindings')
options=('!makeflags')
license=('custom')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/Unidata/netcdf-c/archive/v${pkgver}.tar.gz")
sha256sums=('673936c76ae0c496f6dde7e077f5be480afc1e300adb2c200bf56fbe22e5a82a')

prepare() {
    mkdir -p build
}

build() {
    cd build
    [[ $CARCH != "aarch64" ]] && CDFS="OFF" || CDFS="ON"
    cmake ../${pkgname}-c-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_CDF5=${CDFS} \
        -DENABLE_DAP_LONG_TESTS=ON \
        -DENABLE_EXAMPLE_TESTS=ON \
        -DENABLE_EXTRA_TESTS=ON \
        -DENABLE_FAILING_TESTS=ON \
        -DENABLE_FILTER_TESTING=ON \
        -DENABLE_LARGE_FILE_TESTS=ON
    make
}

check() {
    cd build
    # One test failure https://github.com/Unidata/netcdf-c/issues/808
    make test || warning "Test failure"
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install

    cd "${srcdir}"/${pkgname}-c-${pkgver}
    install -Dm644 COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
