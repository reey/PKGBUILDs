# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set BITS=default instead of MM=" "

pkgname=libbloom
pkgver=1.5
pkgrel=1
pkgdesc="A simple and small bloom filter implementation in plain C"
arch=('x86_64')
url="https://github.com/jvirkki/libbloom"
license=('BSD')
depends=('glibc')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jvirkki/libbloom/archive/v$pkgver.tar.gz")
sha512sums=('0dd69e3ee102e5808ba4350c71d212876db56235e263f8d5a66d6c3616d2e51cc18b5ca3a5a946d79c12751d9cc5667f7c633d77645c4b88a4d16ea344b51929')

build() {
  cd libbloom-$pkgver
  BITS=default make
}

check() {
  cd libbloom-$pkgver
  BITS=default make test
}

package() {
  cd libbloom-$pkgver
  install -Dm644 bloom.h "$pkgdir"/usr/include/bloom.h
  install -Dm755 build/libbloom.so.$pkgver "$pkgdir"/usr/lib/libbloom.so.$pkgver
  ln -s libbloom.so.$pkgver "$pkgdir"/usr/lib/libbloom.so

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
