# Maintainer: Sergej Pupykin <pupykin.s@gmail.com>
# Contributor: damir <damir@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch from Fedora to fix FTBFS

pkgname=libextractor
pkgver=1.7
pkgrel=2
pkgdesc="A library used to extract meta-data from files of arbitrary type"
arch=("x86_64")
license=('GPL')
url="http://gnunet.org/libextractor/"
depends=('libltdl')
makedepends=('exiv2' 'ffmpeg' 'file' 'flac' 'gst-plugins-base-libs' 'libgsf' 'libmpeg2' 'libvorbis' 'poppler')
optdepends=('exiv2: metadata support'
            'ffmpeg: previewopus/thumbnailffmpeg support'
            'file: mime support'
            'flac: flac support'
            'gst-plugins-base-libs: gstreamer support'
            'libarchive: archive support'
            'libgsf: (optional) for ODF support'
            'libmpeg2: mpeg support'
            'libvorbis: ogg/vorbis support'
            'poppler: pdf support')
validpgpkeys=('19647543F7861D3BF4E64FF7BF60708B48426C7E'
              'D8423BCB326C7907033929C7939E6BE1E29FC3CC')
source=("https://ftp.gnu.org/gnu/libextractor/${pkgname}-${pkgver}.tar.gz"{,.sig}
        "libextractor-1.7-segfault.patch")
sha512sums=('00340af0987f58c16824f50a18484e76f110fa2b4a43788b950ea4313e6916b94d5e7f16e1f21c8a54509885d9b44dabdc5be7727547549836ddd8ca7251dbff'
            'SKIP'
            '60f8847b95ae568b55c1ef528472c71166701963a419d3a17afc7359ced4d7d7b9e607410d678412b59ab9fb937e9abc3437d266508200bf6d199271a7574a81')

prepare() {
  sed -e 's/ CODEC_FLAG_QSCALE/ AV_CODEC_FLAG_QSCALE/' \
      -e 's/FF_INPUT_BUFFER_PADDING_SIZE/AV_INPUT_BUFFER_PADDING_SIZE/' \
      -i $pkgname-$pkgver/src/plugins/thumbnailffmpeg_extractor.c
  patch -p0 -d $pkgname-$pkgver -i ../libextractor-1.7-segfault.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/share/info/dir
}
