# Maintainer: Bruno Pagani <archange@archlinux.org> 

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patch to fix ARM FTBFS

pkgname=nsjail
pkgver=2.7
pkgrel=4
pkgdesc="A light-weight process isolation tool"
arch=('x86_64')
url="http://nsjail.com"
license=('Apache')
depends=('protobuf' 'libnl')
makedepends=('git')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/google/nsjail/archive/${pkgver}.tar.gz"
        'git+https://github.com/google/kafel.git#commit=409ccb2113e1f82f497d1dcfb5ae18376ff7056c'
        '26d086f98690646319cf6d7809072f74ccc41795.patch'
        'https://github.com/google/nsjail/commit/4ef480546d9691bd03c5d5b40a9caf4fc815b8e8.patch')
sha256sums=('28afa89c7dbed94528408e4f11d724622d24ea582739b254fa2a739320e87481'
            'SKIP'
            'c7cb56c07832101c2e6aa59cd886b0f7921f140fe07172f83ec8c7413ff5ff64'
            '7782acfdc9d48ad7ddee5e82d028c43d07bf2a2187da8b4f871558d21df36bf4')

prepare() {
    mv kafel ${pkgname}-${pkgver}
    # See https://github.com/google/nsjail/issues/83
    cd ${pkgname}-${pkgver}
    patch -p1 -i ../26d086f98690646319cf6d7809072f74ccc41795.patch
    patch -p1 -i ../4ef480546d9691bd03c5d5b40a9caf4fc815b8e8.patch
}

build() {
    cd ${pkgname}-${pkgver}
    make
}

package() {
    cd ${pkgname}-${pkgver}
    install -Dm755 nsjail -t "${pkgdir}"/usr/bin/ 
}
