# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: BlackEagle <ike.devolder@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - fix fPIC on AArch64

pkgname=clementine
pkgver=1.3.1
pkgrel=20
pkgdesc='A modern music player and library organizer'
url='http://www.clementine-player.org/'
license=('GPL')
arch=('x86_64')
depends=('chromaprint' 'crypto++' 'fftw' 'gcc-libs' 'glew' 'glib2' 'glibc'
         'gst-plugins-base-libs' 'gstreamer' 'libcdio' 'libechonest' 'libgl'
         'libgpod' 'liblastfm' 'libmtp' 'libpulse' 'libx11' 'protobuf' 'qjson'
         'qt4' 'sqlite' 'taglib' 'zlib')
makedepends=('boost' 'cmake' 'git' 'mesa' 'sparsehash')
optdepends=('gst-plugins-base: "Base" plugin libraries'
            'gst-plugins-good: "Good" plugin libraries'
            'gst-plugins-bad: "Bad" plugin libraries'
            'gst-plugins-ugly: "Ugly" plugin libraries'
            'gst-libav: Libav plugin'
            'gvfs: Various devices support')
source=("git+https://github.com/clementine-player/Clementine.git#tag=${pkgver}"
        'clementine-gcc6.patch'
        'clementine-chromaprint1.4.0.patch'
        'clementine-cryptopp6.patch')
sha256sums=('SKIP'
            '05bd4dc0138eed084332fa1a688a96858418731f337f54e0d8ab0853123f40ee'
            '4cc9c9df599df4a595c6acf4e129a54296c0a8e537627435e3df14e1e95447b0'
            'e9c09dca76ed8fae2ecb089571396c9293f546871fb2f27cc37010dd5cba9e46')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  cd Clementine

  # Fix build with GCC 6
  patch -p1 -i ../clementine-gcc6.patch
  # https://github.com/clementine-player/Clementine/pull/5630
  git cherry-pick -n 8a6cc8b5069265e1e92e22def985e22c5955e503
  # Fix build with chromaprint 1.4.0
  patch -p1 -i ../clementine-chromaprint1.4.0.patch
  # Fix build with crypto++ 6.0.0 due to https://github.com/weidai11/cryptopp/issues/442
  patch -p1 -i ../clementine-cryptopp6.patch
  # FS#58413
  git cherry-pick -n -m 1 75f9439843a0e9cdc26cd739d5e1dbd93bd974bc
  # Fix DI.FM
  git cherry-pick -n ddfa5e63f1ef0e243a007efbe576fceb9b2442e8

  if [[ $CARCH == "aarch64" ]]; then
    sed -i 's/fpic/fPIC/' 3rdparty/vreen/vreen/cmake/CommonUtils.cmake
  fi
}

build() {
  cd build

  cmake ../Clementine \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr'\
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -Wno-catch-value"
  make
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}

# vim: ts=2 sw=2 et:
