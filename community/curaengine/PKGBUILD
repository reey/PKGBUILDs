# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Grey Christoforo <first name [at] last name [dot] net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - -DBUILD_TESTS=oFF

pkgname=curaengine
pkgver=3.6.0
pkgrel=1
pkgdesc="Engine for processing 3D models into 3D printing instruction for Ultimaker and other GCode based 3D printers."
url="https://github.com/Ultimaker/CuraEngine"
arch=('x86_64')
license=('AGPL')
depends=('arcus')
# TODO: https://github.com/Ultimaker/CuraEngine/blob/master/CMakeLists.txt#L26
# Package polyclipping in the repos
checkdepends=('cppunit')
makedepends=('cmake' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Ultimaker/${pkgname}/archive/${pkgver}.tar.gz"
       "fix_mocksocket_tests.patch::https://github.com/Ultimaker/CuraEngine/commit/7349c981135f6460640c23527f02dea650304467.patch")
sha512sums=('340dad4c697fb0d7b633091715a904ae7df8973626e217ae2528d0121723c07a6cf0bbc740b9ad9980f3b288fa690c9c3ee43d90914264855dace007bbb334c8'
            '2371a984caec6b9972236f5579a4c928c6cf2c268dd994798b606ba4310333534aea52d2b465d64484081b00ab3550717d4abca3b7174f27f6e1941d43e31cff')

prepare() {
  cd CuraEngine-${pkgver}
  patch -Np1 -i $srcdir/fix_mocksocket_tests.patch
}

build() {
  cd CuraEngine-${pkgver}
  mkdir -p build
  cd build
    
   cmake .. \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DBUILD_TESTS=OFF \
     -DCMAKE_BUILD_TYPE=Release

  make
}

check() {
  cd CuraEngine-${pkgver}/build
  make test
}

package() {
  cd CuraEngine-${pkgver}/build
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
