# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Grey Christoforo <first name [at] last name [dot] net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - -DBUILD_TESTS=oFF

pkgname=curaengine
pkgver=3.4.1
pkgrel=3
pkgdesc="Engine for processing 3D models into 3D printing instruction for Ultimaker and other GCode based 3D printers."
url="https://github.com/Ultimaker/CuraEngine"
arch=('x86_64')
license=('AGPL')
depends=('arcus')
# TODO: https://github.com/Ultimaker/CuraEngine/blob/master/CMakeLists.txt#L26
# Package polyclipping in the repos
checkdepends=('cppunit')
makedepends=('cmake' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Ultimaker/${pkgname}/archive/${pkgver}.tar.gz"
        do_not_copy_resource.patch)
md5sums=('825f80f42da70efe6c889d8acb8f6afe'
         '07b0f2240737f99830c1a87c1f1f378e')

prepare() {
  cd CuraEngine-${pkgver}
  patch -Np1 -i ${srcdir}/do_not_copy_resource.patch
}

build() {
  cd CuraEngine-${pkgver}
  mkdir -p build
  cd build
    
   cmake .. \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DBUILD_TESTS=OFF \
     -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd CuraEngine-${pkgver}/build
  make DESTDIR="${pkgdir}" install
}

check() {
  cd CuraEngine-${pkgver}/build
  make test
}

# vim:set ts=2 sw=2 et:
