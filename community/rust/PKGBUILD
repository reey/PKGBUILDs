# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: userwithuid <userwithuid@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add a link to g++ to compensate for broken cross-compiler decisions
#  - build v6/v7 with -j2 - RAM constraints

buildarch=28
highmem=1

# Remember to bump lib32-rust as well!

pkgname=('rust' 'rust-docs')
epoch=1
pkgver=1.29.0
pkgrel=2

pkgdesc='Systems programming language focused on safety, speed and concurrency'
url='https://www.rust-lang.org/'
arch=('x86_64')
license=('MIT' 'Apache')

makedepends=('rust' 'libffi' 'perl' 'python2' 'curl' 'cmake' 'ninja')

options=('!emptydirs')

source=("https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"{,.asc}
        config.toml
        https://github.com/rust-lang/rust/commit/1b94b84ad0143ea2039610e3aec9e929a8a20733.patch)

sha256sums=('a4eb34ffd47f76afe2abd813f398512d5a19ef00989d37306217c9c9ec2f61e9'
            'SKIP'
            'ff4890bd733509b0dbb885d69afd578c41fc583af256dcc81e9239339c60b144'
            '8c8e79b0bac0dd1434c49f665abfa50634ffb0fbb35c9efee511e5b97960b9e2')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE') # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>

prepare() {
  cd "rustc-$pkgver-src"

  patch -p1 <"$srcdir"/1b94b84ad0143ea2039610e3aec9e929a8a20733.patch
  cp "$srcdir"/config.toml config.toml

  if [[ $CARCH == armv7h ]]; then
    mkdir path
    ln -s /usr/bin/g++ path/arm-linux-gnueabihf-g++
    export PATH="$srcdir/path:$PATH"
  fi
  if [[ $CARCH == armv7h || $CARCH == armv6h ]]; then
    jobs="-j 2"
  fi
}

build() {
  cd "rustc-$pkgver-src"

  msg2 "Run build under x.py"
  python2 ./x.py build $jobs
}

package_rust() {
  depends=('gcc-libs' 'curl' 'libssh2')
  provides=('cargo' 'rustfmt')
  conflicts=('cargo' 'rustfmt')
  replaces=('cargo' 'rustfmt')


  cd "rustc-$pkgver-src"

  DESTDIR="$pkgdir" python2 ./x.py install

  for license in APACHE MIT; do install -Dm644 "LICENSE-$license" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done

  cd "$pkgdir/usr/lib"

  rm rustlib/{components,manifest-rustc,rust-installer-version}
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/*.so .

  # move docs out of the way
  mv "$pkgdir/usr/share/doc" "$srcdir/"

  install -d "$pkgdir"/usr/share/bash-completion
  mv "$pkgdir"/etc/bash_completion.d/ "$pkgdir"/usr/share/bash-completion/completions/
}

package_rust-docs() {
  msg2 "Packaging documentation"
  install -d "$pkgdir/usr/share/doc/"
  mv "$srcdir"/doc/* "$pkgdir"/usr/share/doc/rust/

  msg2 "Packaging license files for the documentation"
  for license in APACHE MIT; do install -Dm644 "rustc-$pkgver-src/LICENSE-$license" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done
}

# vim:set ts=2 sw=2 et:
