# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with armv8-a CRC extensions

buildarch=8

pkgname=wiredtiger
pkgver=3.1.0.20181030
_commit=231a81ea33bf2ecfb084099ff9af434548d15d54
pkgrel=1
pkgdesc="High performance, scalable, production quality, NoSQL, Open Source extensible platform for data management"
arch=('x86_64')
url="http://source.wiredtiger.com/"
license=('GPL')
depends=('snappy' 'lz4' 'zlib' 'gperftools')
source=("$pkgname-$_commit.tar.gz::https://github.com/wiredtiger/wiredtiger/archive/$_commit.tar.gz")
sha512sums=('f38e43624d93799d92829fe59c7635a84e30ded0eb1474b90ef98da94f793d3ce9840a6cee690a71e96ae1f2d4c2a256c37c387e6907076d8c21a0bed13b0191')

prepare() {
  mv wiredtiger-{$_commit,$pkgver}
}

build() {
  cd wiredtiger-$pkgver

  CFLAGS=`echo $CFLAGS | sed -e 's/armv8-a/armv8-a+crc/'` && CXXFLAGS="$CFLAGS"
  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-leveldb \
              --enable-lz4 \
              --enable-tcmalloc \
              --enable-verbose \
              --with-builtins=snappy,zlib
  make
}

check() {
  cd wiredtiger-$pkgver
  make test
}

package() {
  cd wiredtiger-$pkgver
  make DESTDIR="$pkgdir" install
}
