# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with armv8-a CRC extensions

buildarch=8

pkgname=wiredtiger
pkgver=3.0.0.20180814
_commit=aeeee2053a380ebd2ba90f606a56176737882aad
pkgrel=1
pkgdesc="High performance, scalable, production quality, NoSQL, Open Source extensible platform for data management"
arch=('x86_64')
url="http://source.wiredtiger.com/"
license=('GPL')
depends=('snappy' 'lz4' 'zlib' 'gperftools')
source=("$pkgname-$_commit.tar.gz::https://github.com/wiredtiger/wiredtiger/archive/$_commit.tar.gz")
sha512sums=('03c312b5c748843faeceaf2c999c90a92be7f89a1701197cd5cd1a6a7cf5de750f0421e88648fa277c98374ca41a1b63acf5a8707d6181f5f77d13ae364dea64')

prepare() {
  mv wiredtiger-{$_commit,$pkgver}
}

build() {
  cd wiredtiger-$pkgver

  CFLAGS=`echo $CFLAGS | sed -e 's/armv8-a/armv8-a+crc/'` && CXXFLAGS="$CFLAGS"
  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-leveldb \
              --enable-lz4 \
              --enable-tcmalloc \
              --enable-verbose \
              --with-builtins=snappy,zlib
  make
}

check() {
  cd wiredtiger-$pkgver
  make test
}

package() {
  cd wiredtiger-$pkgver
  make DESTDIR="$pkgdir" install
}
