# Maintainer : Sven-Hendrik Haase <sh@lutzhaase.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=openvdb
pkgver=5.2.0
pkgrel=1
pkgdesc='A large suite of tools for the efficient storage and manipulation of sparse volumetric data discretized on three-dimensional grids'
url='https://github.com/dreamworksanimation/openvdb'
arch=('x86_64')
license=('MPL')
depends=('openexr' 'boost-libs' 'intel-tbb' 'zlib' 'jemalloc' 'blosc')
makedepends=('doxygen' 'boost' 'cmake' 'mesa' 'cppunit' 'glfw-x11' 'glu' 'python-numpy')
optdepends=('glfw: for tools'
            'glu: for tools'
            'python-numpy: python module')
source=("https://github.com/dreamworksanimation/openvdb/archive/v${pkgver}.tar.gz"
        'boost-1.67.patch')
sha512sums=('69e21190c27a055e476a319b0c2d961303fa2f3fec30bec93ed44072d01816ef89e5e8f0abfc3a22d1c73742aa31284c664596747b49cd985fea1a751776506e'
            '48075304517a9e0f52d5c22c575a22fb7904c743262652d4abe1028bfc540d4f5a8a97bd754acbb3959a7c148d12c7e55a71bd630e59bf4fe51433ac9dca4372')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ../boost-1.67.patch
  sed -i "s|DESTINATION docs|DESTINATION share/doc/$pkgname|" openvdb/CMakeLists.txt
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  [[ -d build ]] && rm -rf build
  mkdir build && cd build

  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && LDFLAGS="-latomic $LDFLAGS"
  export CXXFLAGS="${CXXFLAGS} -DPY_OPENVDB_USE_NUMPY"
  cmake .. \
    -DBLOSC_LOCATION=/usr/ \
    -DTBB_LOCATION=/usr/ \
    -DUSE_GLFW3=ON \
    -DGLFW3_LOCATION=/usr/ \
    -DILMBASE_NAMESPACE_VERSIONING=OFF \
    -DOPENEXR_NAMESPACE_VERSIONING=OFF \
    -DILMBASE_LOCATION=/usr/ \
    -DOPENEXR_LOCATION=/usr/ \
    -DCPPUNIT_LOCATION=/usr/include/cppunit \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOPENVDB_BUILD_PYTHON_MODULE=ON \
    -DOPENVDB_BUILD_DOCS=ON \
    -DOPENVDB_ABI_VERSION_NUMBER=3 \
    -DOPENVDB_BUILD_UNITTESTS=OFF

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  make DESTDIR="${pkgdir}" install
}

# vim:set sw=2 sts=2 et:
