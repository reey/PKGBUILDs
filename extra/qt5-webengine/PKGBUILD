# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for chromium GN
#  - patch for bundled gn

buildarch=12
highmem=1

pkgname=qt5-webengine
_qtver=5.12.0
pkgver=${_qtver/-/}
pkgrel=1
arch=('x86_64')
url='http://qt-project.org/'
license=('LGPL3' 'LGPL2.1' 'BSD')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel' 'qt5-location' 'libxcomposite' 'libxrandr' 'pciutils' 'libxss' 
         'libevent' 'snappy' 'nss' 'protobuf' 'libxslt' 'minizip' 'ffmpeg' 're2' 'libvpx')
makedepends=('python2' 'git' 'gperf' 'jsoncpp' 'ninja' 'qt5-tools' 'poppler')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
         qtwebengine-harmony.patch
         qtbug-71370.patch::"http://code.qt.io/cgit/qt/qtwebengine.git/patch/?id=20238f2c"
         qtbug-69605.patch::"http://code.qt.io/cgit/qt/qtwebengine.git/patch/?id=721cd2d2"
         0001-ARM-toolchain-fixes.patch
         0001-Add-ARM-headers-back.patch)
sha256sums=('bd581e390a30e0f74d41b0e3334b3cf612dd4af23de36a3bf5931d5b4453687c'
            'feca54ab09ac0fc9d0626770a6b899a6ac5a12173c7d0c1005bc3964ec83e7b3'
            '58aaec357311fcf72b1d94c40f5159b84c835bbf41fcf9a0977368c99bea70f4'
            '8f44545a6acd1bc58c7ddd8ff369a818102b6a1fecd132eb2508b18fd1433d8b'
            '8202b09a1caa82538a2eacd79b62b61d8661c65cdfb275560d231aa31a362b12'
            '7fbafe97a1e6b607dbd9325f426217fd7051d4d6c8dc33df1cc388f416007c4d')

prepare() {
  mkdir -p build

  # Hack to force using python2
  mkdir -p bin
  ln -s /usr/bin/python2 bin/python

  cd ${_pkgfqn}

  # FreeType 2.8.1
  patch -Np1 -i ../qtwebengine-harmony.patch
  # https://bugreports.qt.io/browse/QTBUG-71370
  patch -p1 -i ../qtbug-71370.patch
  # https://bugreports.qt.io/browse/QTBUG-69605
  patch -p1 -i ../qtbug-69605.patch

  cd src/3rdparty
  patch -p1 -i ${srcdir}/0001-ARM-toolchain-fixes.patch
  patch -p1 -d gn -i ${srcdir}/0001-Add-ARM-headers-back.patch
}

build() {
  cd build

  export PATH="$srcdir/bin:$PATH"
  qmake ../${_pkgfqn} -- \
    -proprietary-codecs \
    -system-ffmpeg \
    -webp \
    -spellchecker \
    -webengine-icu
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -Dm644 "$srcdir"/${_pkgfqn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium
}
