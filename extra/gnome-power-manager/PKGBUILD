# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to enable linking with libm to fix FTBFS

pkgname=gnome-power-manager
pkgver=3.30.0
pkgrel=1
pkgdesc="System power information and statistics"
url="https://gitlab.gnome.org/GNOME/gnome-power-manager"
arch=(x86_64)
license=(GPL)
depends=(gtk3 upower dconf)
makedepends=(git meson)
_commit=de5ca85cc67df273170bf4f28febdb9ebfd92cf1  # tags/GNOME_POWER_MANAGER_3_30_0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-power-manager.git#commit=$_commit"
        '0001-set-libm-as-required.patch')
sha256sums=('SKIP'
            '6bbc57044aa78979137a3ee7ece5ff37c7bea5796d69e1c3a4c91c11a7b33d67')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_POWER_MANAGER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  git apply ../0001-set-libm-as-required.patch
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
