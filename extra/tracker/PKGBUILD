# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable functional tests

pkgname=tracker
pkgver=2.1.6
pkgrel=2
pkgdesc="Desktop-neutral user information store, search tool and indexer"
url="https://wiki.gnome.org/Projects/Tracker"
arch=(x86_64)
license=(GPL)
depends=(sqlite icu glib2 libffi util-linux libstemmer libseccomp libsoup json-glib upower libnm)
makedepends=(gobject-introspection intltool vala git gtk-doc bash-completion meson)
checkdepends=(python2-gobject python2-dbus)
groups=(gnome)
_commit=c2d3289a13092642ec9735e0b2d508592532483d  # tags/2.1.6^0
source=("git+https://gitlab.gnome.org/GNOME/tracker.git#commit=$_commit"
        0001-build-Restore-right-soversion-to-libraries.patch)
sha256sums=('SKIP'
            '13dbef758222723729a55de04a98488b5b943e565e6cbde25eb4f5598a6729c3')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../0001-build-Restore-right-soversion-to-libraries.patch
}

build() {
  # LTO breaks due to duplicate types:
  # https://gitlab.gnome.org/GNOME/tracker/issues/44
  arch-meson $pkgname build -D b_lto=false -Dfunctional_tests=false
  ninja -C build
}

check() {
  # Many tests are broken:
  # - Assume schemas are installed
  # - Assume services are autolaunched
  dbus-run-session meson test -C build || :
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
