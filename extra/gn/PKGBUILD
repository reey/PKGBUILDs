# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add ARM headers back that were removed upstream

buildarch=28

pkgname=gn
pkgver=0.1480.a676103b
_commit=a676103b8180564c6d7f8330201f9b5fe102e3f4
pkgrel=1
pkgdesc="Meta-build system that generates build files for Ninja"
arch=('x86_64')
url="https://gn.googlesource.com/gn/+/master/README.md"
license=('BSD')
depends=('gcc-libs')
makedepends=('clang' 'ninja' 'python2' 'git')
source=(git+https://gn.googlesource.com/gn#commit=$_commit
        0001-Add-ARM-headers-back.patch)
md5sums=('SKIP'
         '9948af7a864a8e57697c49d15cdcb395')

pkgver() {
  cd $pkgname
  echo 0.$(git rev-list --count initial-commit..).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname
  patch -p1 -i ../0001-Add-ARM-headers-back.patch
}

build() {
  cd $pkgname
  python2 ./build/gen.py --no-sysroot
  ninja -C out gn
}

package() {
  cd $pkgname
  install -D out/gn "$pkgdir/usr/bin/gn"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
