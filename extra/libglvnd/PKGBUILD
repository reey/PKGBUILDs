# $Id: PKGBUILD 249779 2015-10-27 00:47:25Z eric $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable assembly on !armv7h

pkgname=libglvnd
pkgver=1.1.0
pkgrel=1
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
makedepends=('libxext' 'libx11' 'glproto' 'python')
provides=('libgl' 'libegl' 'libgles')
source=("$pkgname-$pkgver.tar.gz::https://github.com/NVIDIA/libglvnd/archive/v$pkgver.tar.gz"
        LICENSE)
sha512sums=('40c81f334f44927af4ebdd291f4447d4c01cbaa9f39ebcd519c86e360357b7427b574514a70b62c9538dc90fa312ed632f49cc5e818a800e23b250ec92f4ae29'
            'bf0f4a7e04220a407400f89226ecc1f798cc43035f2538cc8860e5088e1f84140baf0d4b0b28f66e4b802d4d6925769a1297c24e1ba39c1c093902b2931781a5')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh
  [[ $CARCH != "armv7h" ]] && CONFIG="--disable-asm"
  ./configure --prefix=/usr $CONFIG
  make
}

package() {
  # libglvnd needs mesa for indirect rendering
  depends=('libxext' 'mesa' 'opengl-driver')

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
