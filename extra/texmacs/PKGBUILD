# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch> # Contributor: Christopher Reimer <c.reimer1993@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix size_t check on ARM

pkgname=texmacs
pkgver=1.99.8
pkgrel=1
pkgdesc="Free scientific text editor, inspired by TeX and GNU Emacs. WYSIWYG editor TeX-fonts and CAS-interface (Giac, GTybalt, Macaulay 2, Maxima, Octave, Pari, Qcl, R and Yacas) in one."
arch=('x86_64')
url="http://www.texmacs.org/"
license=('GPL')
depends=('perl' 'guile1.8' 'texlive-core' 'python2' 'libxext' 'freetype2' 'qt5-base')
# do not remove texlive-core dependency, as it is needed!
optdepends=('transfig: convert images using fig2ps'
            'gawk: conversion of some files'
	    'ghostscript: rendering ps files'
            'imagemagick: convert images'
            'aspell: spell checking')
makedepends=('ghostscript' 'cmake')
source=(http://www.texmacs.org/Download/ftp/tmftp/source/TeXmacs-${pkgver}-src.tar.gz
        texmacs-fix-linking.patch
        fix-size_t-check.patch)
options=('!emptydirs')
sha256sums=('7febdbccc6d1ac16d9375a136814259499fb38a49078de2857ab4f47d29a20c0'
            '8b52633981cf71357064e816f898386a4aed3d424f3f417da8dc77c4df094e0f'
            'c7ea32216be57e8cf29109c89b47afd55b2c1e6c4bc31e377ca9d278cde18f09')
	
prepare() {
  cd TeXmacs-${pkgver}-src

  sed -i 's/env python/env python2/' \
    plugins/{mathematica/bin/realpath.py,python/bin/tm_python,sage/bin/tm_sage} \
    TeXmacs/misc/inkscape_extension/texmacs_reedit.py
  sed -i 's/"python"/"python2"/' plugins/python/progs/init-python.scm

  patch -p1 -i ../texmacs-fix-linking.patch # Fix linking

  patch -p1 -i ../fix-size_t-check.patch
}

build() {
  cd TeXmacs-${pkgver}-src
  
  mkdir -p build
  cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGUILECONFIG_EXECUTABLE=/usr/bin/guile-config1.8
  make
}

package() {
  cd TeXmacs-${pkgver}-src/build
  make DESTDIR=${pkgdir} install

 # fix fig2ps script
 sed -i 's|${prefix}|/usr|' "${pkgdir}/usr/bin/fig2ps"
 # fix launch script
 sed -e 's|\@CONFIG_LIB_PATH\@|LD_LIBRARY_PATH|g' -i "$pkgdir"/usr/bin/texmacs
}
