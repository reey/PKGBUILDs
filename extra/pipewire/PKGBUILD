# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on valgrind

pkgname=pipewire
pkgver=0.2.3
pkgrel=1
pkgdesc="Server and user space API to deal with multimedia pipelines"
url="https://pipewire.org"
license=(LGPL2.1)
arch=(x86_64)
depends=(gstreamer gst-plugins-base v4l-utils sbc ffmpeg rtkit)
makedepends=(git meson doxygen graphviz xmltoman)
install=pipewire.install
_commit=8d53d3018f2d3bccd72bffb901ec36095d430382  # tags/0.2.3
source=("git+https://github.com/PipeWire/pipewire#commit=$_commit"
        pkgconfig.diff)
sha256sums=('SKIP'
            'c4e145b4e18aa2d218b8f8ce96d5ba01626599e9349955226300b17779817fb5')


pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  patch -Np1 -i ../pkgconfig.diff

  # Reduce docs size
  printf '%s\n' >>doc/Doxyfile.in \
    HAVE_DOT=yes DOT_IMAGE_FORMAT=svg INTERACTIVE_SVG=yes
}

build() {
  arch-meson $pkgname build \
    -D docs=true \
    -D man=true \
    -D gstreamer=true
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
