# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - configure needs --disable-asm to build on non-AArch64
#  - --disable-lto

pkgname=x264
pkgver=155.r0a84d986
pkgrel=2
epoch=2
pkgdesc='Open Source H264/AVC video encoder'
arch=('x86_64')
url='https://www.videolan.org/developers/x264.html'
license=('GPL')
depends=(
  'libavcodec.so' 'libavformat.so' 'libavutil.so' 'liblsmash.so'
  'libswscale.so'
)
makedepends=('git' 'ffmpeg' 'l-smash' 'nasm')
provides=('libx264' 'libx264.so')
conflicts=('libx264' 'libx264-10bit' 'libx264-all')
replaces=('libx264' 'libx264-10bit' 'libx264-all')
_commit='0a84d986e7020f8344f00752e3600b9769cc1e85'
source=("git+https://git.videolan.org/git/x264.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd x264

  local _ver=$(grep '#define X264_BUILD' x264.h | cut -d' ' -f3)
  local _rev=$(git rev-parse --short HEAD)

  echo ${_ver}.r${_rev}
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-asm"

  ../x264/configure \
    --prefix='/usr' \
    --enable-shared \
    --enable-pic \
    --disable-lto $CONFIG
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install-cli install-lib-shared
}

# vim: ts=2 sw=2 et:
