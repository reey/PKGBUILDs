# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Balló György <ballogyor+arch@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to remove 'debug' meson option for compatibility with meson 0.48

pkgname=libsignon-glib
pkgver=2.0
pkgrel=1
pkgdesc='GLib-based client library for applications handling account authentication through the Online Accounts Single Sign-On service'
arch=(x86_64)
url='https://gitlab.com/accounts-sso/libsignon-glib'
license=(LGPL2.1)
depends=(signond)
makedepends=(python-gobject gtk-doc gobject-introspection vala meson git)
source=("git+https://gitlab.com/accounts-sso/${pkgname}#tag=VERSION_$pkgver"
        '0001-remove-meson-debug-option.patch')
sha256sums=('SKIP'
            'ff25c4d082bbc28af1dc33094867affc6d900390d528e53a584a4aa6b6c01b66')

prepare() {
  mkdir -p build

  cd $pkgname
  git submodule init
  git submodule update

  git apply ../0001-remove-meson-debug-option.patch
}

build() {
  cd build
  arch-meson ../$pkgname -Dtests=false
  ninja -j1
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
